package de.monticore.lang;

/**
 * @author Robin Wolters
*/

grammar SD extends de.monticore.common.Common { 

    /*========================================================================*/
    /*================================ TODOs =================================*/
    /*========================================================================*/
    
    // OCL-Integration
    // static Referenz zu Klassen: 
    // -> Vllt nicht nur auf Naming achten, sondern explizit als "class" benennen
    
    // sequencediagram for ... (SCs) using ... (CDs) ... initial (OD)
    // -> oder in aufbauende Sprache?
    // Stereotypen wie <<match:complete>> auch in "SD Fähnchen"?
    // Aktivitätsbalken
       
    /*========================================================================*/
    /*=============================== TOKENS =================================*/
    /*========================================================================*/
    
    token NameWithExtras = (Name? '-' Name?)+ ;
      
    /*========================================================================*/
    /*=========================== CompilationUnit ============================*/
    /*========================================================================*/
    
    SDCompilationUnit = 
    	PackageDeclaration?
    	ImportStatement*
    	SequenceDiagram;
    	
    PackageDeclaration = 
    	"package" 
    	QualifiedName
    	";";
    	
    ImportStatement = 
    	"import"
    	QualifiedName
    	";";

    /*========================================================================*/
    /*================== INTERFACES AND EXTERNAL SYMBOLS =====================*/
    /*========================================================================*/

    interface Interaction;

    /*========================================================================*/
    /*============================ ENUMERATIONS ==============================*/
    /*========================================================================*/
    
    enum Arrow = 
    	LEFT: "<-"  | RIGHT: "->" ;
    enum DashedArrow = 
    	LEFT: "<--" | RIGHT: "-->";

    /*========================================================================*/
    /*=================== Global Sequence Diagram Stuff ======================*/
    /*========================================================================*/

	symbol scope SequenceDiagram = 
		"sequencediagram" Name "{" 
		(ObjectDeclaration ";")*
		SDElement* 
		"}";
		
	symbol ObjectDeclaration = 
		Stereotype?
		SDCompleteness?
		(	Name |
			Name ":" ofType:Name |
			":" ofType:Name
		);	
	
	SDElement = 
		(Interaction ";") | 
		OCLBlock | 
		Java;
		
	ObjectReference = 
		Name | 
		"(" ObjectDeclaration ")";
		
	SDCompleteness =
		[complete :"(C)"] |
		[free	  :"..."] |
		[complete :"<<match:complete>>"]|
		[free     :"<<match:free>>"] 	|
		[visible  :"<<match:visible>>"] |
		[initial  :"<<match:initial>>"];
	

	
    /*========================================================================*/
    /*=========================== Interactions ===============================*/
    /*========================================================================*/

	
	MethodCall implements Interaction = 
		left:ObjectReference 
		Arrow 
		right:ObjectReference 
		":" 
		Stereotype?
		Method;
		
	Return implements Interaction = 
		left:ObjectReference 
		DashedArrow 
		right:ObjectReference 
		":" 
		ReturnStatement?;
	
	Exception implements Interaction = 
		left:ObjectReference
		DashedArrow 
		right:ObjectReference 
		":" 
		Name
		Args?;
		
	Method = 
		StaticModifier?
		Name 
		Args?;
		
	Constructor extends Method =
		"new" 
		Name
		Args?;
				
	StaticModifier = 
		["static"]?;
				
	Args = 
		"("
		ParamList?
		")";
		
	ParamList =
		incomplete:["..."] |
		( (Param ",")* Param);
		
	Param = 
		String | 
		Name | 
		Num_Int;
	
	ReturnStatement = 
		"return" 
		Result?;
	
	Result = 
		incomplete:["..."] |
		QualifiedName;
		
    /*========================================================================*/
    /*=================== Integration of other Languages =====================*/
    /*========================================================================*/
	
	OCLBlock = 
		"<" 
		(context:Name ":")? 
		"["
		SDOCLExpression
		"]"
		">";
		
	SDOCLExpression = 
		String;
	
	Java = 
		"{" String "}";

    
}