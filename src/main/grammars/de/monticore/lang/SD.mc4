package de.monticore.lang;

/**
 * @author Robin Wolters
*/

grammar SD extends de.monticore.common.Common { 

    /*========================================================================*/
    /*================================ TODOs =================================*/
    /*========================================================================*/
    
    // CHANGE: Object Declaration not an element, but only at the beginning
    // sequencediagram for ... (SCs) using ... (CDs) ... initial (OD)
    // -> oder in aufbauende Sprache?
    // Stereotypen in "SD Fähnchen"?
    // Matching Stereotypen in Objekten
    // Aktivitätsbalken
       
    /*========================================================================*/
    /*=============================== TOKENS =================================*/
    /*========================================================================*/
    
    token NameWithExtras = (Name? '-' Name?)+ ;
      
    /*========================================================================*/
    /*=========================== CompilationUnit ============================*/
    /*========================================================================*/
    
    SDCompilationUnit = 
    	PackageDeclaration?
    	ImportStatement*
    	SequenceDiagram;
    	
    PackageDeclaration = 
    	"package" 
    	QualifiedName
    	";";
    	
    ImportStatement = 
    	"import"
    	QualifiedName
    	";";

    /*========================================================================*/
    /*================== INTERFACES AND EXTERNAL SYMBOLS =====================*/
    /*========================================================================*/

    interface Interaction;

    /*========================================================================*/
    /*============================ ENUMERATIONS ==============================*/
    /*========================================================================*/
    
    enum Arrow = 
    	LEFT: "<-"  | RIGHT: "->" ;
    enum DashedArrow = 
    	LEFT: "<--" | RIGHT: "-->";

    /*========================================================================*/
    /*=================== Global Sequence Diagram Stuff ======================*/
    /*========================================================================*/

	SequenceDiagram = 
		"sequencediagram" Name "{" 
		(ObjectDeclaration ";")*
		SDElement* 
		"}";
		
	symbol ObjectDeclaration = 
		Stereotype?
		(	Name |
			Name ":" ofType:Name |
			":" ofType:Name
		);	
	
	SDElement = 
		(Interaction ";") | 
		OCL | 
		Java;
		
	ObjectReference = 
		Name | 
		"(" ObjectDeclaration ")";
		
	// special SD-stereotypes
	// TODO
	
    /*========================================================================*/
    /*=========================== Interactions ===============================*/
    /*========================================================================*/

	
	MethodCall implements Interaction = 
		left:ObjectReference 
		Arrow 
		right:ObjectReference 
		":" 
		Stereotype?
		Method;
		
	Return implements Interaction = 
		left:ObjectReference 
		DashedArrow 
		right:ObjectReference 
		":" 
		ReturnStatement?;
	
	Exception implements Interaction = 
		left:ObjectReference
		DashedArrow 
		right:ObjectReference 
		":" 
		Name
		Args?;
		
	Method = 
		StaticModifier?
		Name 
		Args?;
		
	Constructor extends Method =
		"new" 
		Name
		Args?;
				
	StaticModifier = 
		["static"]?;
				
	Args = 
		"("
		ParamList?
		")";
		
	ParamList =
		incomplete:["..."] |
		( (Param ",")* Param);
		
	Param = 
		String | 
		Name | 
		Num_Int;
	
	ReturnStatement = 
		"return" 
		Result?;
	
	Result = 
		incomplete:["..."] |
		QualifiedName;
		
    /*========================================================================*/
    /*=================== Integration of other Languages =====================*/
    /*========================================================================*/
	
	OCL = 
		"<" String ">";
	
	Java = 
		"{" String "}";

    
}