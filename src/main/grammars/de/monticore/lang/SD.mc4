package de.monticore.lang;

grammar SD extends de.monticore.common.Common { 

    /*========================================================================*/
    /*================================ TODOs =================================*/
    /*========================================================================*/
    
    // Stereotypen & Mehr in "SD FÃ¤hnchen"
    // Complete und Incomplete in Objekten
    // Match:complete, ... in SD
      
    /*========================================================================*/
    /*=========================== CompilationUnit ============================*/
    /*========================================================================*/

    /*========================================================================*/
    /*================== INTERFACES AND EXTERNAL SYMBOLS =====================*/
    /*========================================================================*/

    /*========================================================================*/
    /*=============================== TOKENS =================================*/
    /*========================================================================*/

    /*========================================================================*/
    /*============================ ENUMERATIONS ==============================*/
    /*========================================================================*/
    
    enum Arrow = LEFT: "<-" | RIGHT: "->";
    enum DashedArrow = LEFT: "<--" | RIGHT: "-->";
    Modifier = static: "static";

    /*========================================================================*/
    /*============================== Grammar =================================*/
    /*========================================================================*/

	SequenceDiagram = 
		"sequencediagram" Name "{" 
		SDElement* 
		"}";
	
	SDElement = 
		(ObjectDeclaration ";") | 
		(Interaction ";") | 
		OCL | 
		Java ;
		
	
	ObjectDeclaration = (	
		(Name ":" ofType:Name) | 
		Name |
		(":" ofType:Name)
		) ;
	
	Interaction = 
		MethodCall |
		Return | 
		Exception ;
	
	MethodCall = 
		source:Object Arrow target:Object ":" 
		Stereotype? 
		(Method | Constructor);
		
	Object = 
		Name |
		( "(" ObjectDeclaration ")" );
		
	Stereotype = 
		"<<" Name ">>";
		
	Method = 
		Modifier? 
		Name 
		Args?;
		
	Constructor =
		"new" 
		Name 
		Args?;
		
	Args = 
		"("
		Params?
		")";
		
	Params =
		"..." |
		( (Param ",")* Param);
		
	Param = String | Name;
	
	Return = 
		target:Object DashedArrow source:Object ":" 
		"return"? Result? ;
		
	Result = 
		"..." |
		Name;
	
	Exception = 
		source:Object DashedArrow target:Object ":" 
		Method;
	
	OCL = 
		"<" String ">";
	
	Java = 
		"{" String "}";

    
}