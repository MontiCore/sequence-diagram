package de.monticore.lang;

component grammar SD4Code extends de.monticore.lang.SDBase,
                                  de.monticore.lang.BasicSDModifiers,
                                  de.monticore.expressions.CommonExpressions {

  MethodInvocationAction implements Action =
    MethodModifier* ["static"]? Name Arguments?;

  /**
  * Extension point for MethodModifiers for MethodInvocationActions.
  */
  interface MethodModifier;

  TriggerModifier implements MethodModifier = "trigger";

  ConstructorAction implements Action =
    "new" MCQualifiedName Arguments?;

  interface EndOfMethodAction extends Action;

  ReturnAction implements EndOfMethodAction =
    ("return" Expression)?;

  ExceptionAction implements EndOfMethodAction =
    "throw" MCQualifiedName Arguments?;

  EndOfMethodInteraction implements Interaction =
    ((from:InteractionEntity "-->" to:InteractionEntity?) |
     (to:InteractionEntity? "<--" from:InteractionEntity))
    ":" Action; // only EndOfMethodActions are allowed.

  /**
   * Used for incomplete specifications of (method, exception, return, etc.) arguments.
   */
  IncompleteExpression implements Expression =
    "...";

  ClassInteractionEntity implements InteractionEntity = "class" MCObjectType;
}
