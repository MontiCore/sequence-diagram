package de.monticore.lang;

component grammar SDBasis extends de.monticore.types.MCBasicTypes,
                                  de.monticore.types.BasicTypeSymbols,
                                  de.monticore.UMLStereotype {

  SDArtifact =
    ("package" MCQualifiedName& ";")?
    MCImportStatement*
    SequenceDiagram;

  symbol scope SequenceDiagram =
    Stereotype? SDModifier* "sequencediagram" Name "{"
      (SDNamedObject | SDAnonymousObject)*
      SDElement*
    "}";


  /*
   * Common match-modifiers for sequence diagrams and objects.
   */
  enum SDModifier =
    COMPLETE:"(c)" | "complete" | FREE:"..." | "free" | "initial" | "visible";

  /**
   * Object definition or usage.
   * Symboltablecreator creates instances of VariableSymbol for named objects.
   */
  SDNamedObject implements Variable =
    Stereotype? SDModifier* Name (":" MCObjectType)? ";";

  SDAnonymousObject =
    Stereotype? SDModifier* ":" MCObjectType ";";

  interface SDElement;

  /*
   * Extension point for Interactions.
   * To enable a uniform handling of interactions, the fields
   * "source" defining the source of the interaction, "target"
   * defining the target of the interaction, and "action" defining
   * the action of the interaction are required.
   */
  interface SDInteraction extends SDElement =
    SDSource? SDTarget? SDAction;

  /*
   * Possible sources of interactions.
   */
  interface SDSource;
  SDObjectSource implements SDSource = Name@Variable;

  /*
   * Possible targets of interactions.
   */
  interface SDTarget;
  SDObjectTarget implements SDTarget = Name@Variable;

  interface SDAction;

  /*
   * Sending messages.
   * Including the curly brackets starts an activity bar on the lifeline
   * of the target object that ends with the closing curly bracket.
   */
  SDSendMessage implements SDInteraction =
    SDSource? "->" SDTarget? ":" SDAction (";" | "{" SDElement* "}");
}